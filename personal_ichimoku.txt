//@version=3
//The files in this repository are created by me and provided under the MIT License
//located at https://github.com/yield65/tradingview/blob/master/LICENSE
//If you find them useful please consider making a donation, thank you.
//Bitcoin: 3F636VrPCdnbfrdP5kS4C6fHWVBffXNKCu
//Litecoin: 33932ckE7i3oAQZxxSgLcvmbn8RAgTc2tk
//ETH: 0x53A43EF9E56908A9c868FBf2f1b9DE7B3486FDAF
//contact: bucket@mailbox.org

study(title="Ichimoku + ALMA + BB", overlay=true, precision=0)

tenkanSen = isintraday ? 8 : 9
kijunSen = isintraday ? 34 : 26
chikouSen = isintraday ? 13 : 52
senkou = isintraday ? 55 : 52
displacement = isintraday ? 21 : 52
chikou_d = input(false, title="Display Chikou?")
showxobb = input(true, title="%BB bounds?")
maxo = input(false, title="Show ALMAs?")
showvwap = input(false, title="Show VWAP?")
display_ma=input(false, "Display EMA Fibs?", type=bool)
display_bitmex_futures=input(false, "Display BMX QTLY?", type=bool)
display_bb=input(false, "Display BBs?", type=bool)
usevwma = input(false, "Use VWMA for BB?(ALMA)", type=bool)
bbmodel = input(1, minval=1, maxval=3, title="BB Model")

//Note: change and update futures accordingly
bitmex_futures=security("BITMEX:XBTM18", period, close)

offset = input(0.75, title="ALMA offset", step=0.05)
sigma = input(4, title="ALMA sigma", step=1)

plot(display_bitmex_futures ? bitmex_futures : na, color = #3c78d8, linewidth = 3, title='XBTFutures', transp=0)


//====BB====
bbcolor1 = #9D57E8
bbcolor2 = #E34CB4
bbcolor3 = #1874CD
mult = bbmodel == 1 ? 2.0 : bbmodel == 2 ? 1.5 : 2.5
bblen = bbmodel == 1 ? 21 : bbmodel == 2 ? 10 : 50
basis = usevwma ? vwma(hl2, bblen) : alma(hl2, bblen, 0.75, 4)
dev = mult * stdev(hl2, bblen)
upper = basis + dev
lower = basis - dev
plot(display_bb ? basis : na, color=bbcolor1, style=line, linewidth=2, transp=0, editable=false)
p1bb = plot(display_bb ? upper : na, color=bbcolor2, transp=0, linewidth=2, editable=false)
p2bb = plot(display_bb ? lower :na, color=bbcolor2, transp=0, linewidth=2, editable=false)
fill(p1bb, p2bb, color=bbcolor3, transp=75, editable=true, title="BB Fill")
//====BB====

darkorange = #FF8C00

donchian(len) => avg(lowest(len), highest(len))
tenkan = donchian(tenkanSen)
kijun = donchian(kijunSen)
chikouline = linreg(hl2, 10, 0)
senkouA = avg(tenkan, kijun)
senkouB = donchian(senkou)

bear = #DB7093
bull = #98FB98
bearcloud = #DB7093
bullcloud = #98FB98

p1 = plot(senkouA, title = 'Senkou Span A', offset = displacement, color=bull, transp=60, linewidth=1, editable=false)
p2 = plot(senkouB, title = 'Senkou Span B', offset = displacement, color=bear, transp=60, linewidth=1, editable=false)
fill(p1, p2, color = senkouA > senkouB ? bullcloud : bearcloud, transp=80, title="Senkou fill", editable=false)

plot(chikou_d ? chikouline : na, title='Chikou Span', linewidth=2, offset=-displacement, color=darkorange, transp=0, editable=true)

//ALMA MAs
length0 = input(13)
length1 = input(34)
length2 = input(55)
length3 = input(89)
ma0 = alma(close, length0, offset, sigma)
ma1 = alma(close, length1, offset, sigma)
ma2 = alma(close, length2, offset, sigma)
ma3 = alma(close, length3, offset, sigma)
maxobull = close > ma0 and ma0 > ma1 and ma1 > ma2 and ma2 > ma3
maxobear = close < ma0 and ma0 < ma1 and ma1 < ma2 and ma2 < ma3
plot(maxo ? ma0 : na, color=aqua, transp=0, linewidth=2, editable=false)
plot(maxo ? ma1 : na, color=yellow, transp=0, linewidth=2, editable=false)
plot(maxo ? ma2 : na, color=fuchsia, transp=0, linewidth=2, editable=false)
plot(maxo ? ma3 : na, color=red, transp=0, linewidth=2, editable=false)

//Fib EMAs

ma1_color = #AAFFFF
ma2_color = #8DEEC2
ma3_color = #72DD7E
ma4_color = #80CC5A
ma5_color = #A0BB45
ma6_color = #A98F32
ma7_color = #994922
ma8_color = #871421
ma9_color = #770846
ma10_color = #660066

plot(display_ma ? ema(close,8) : na, color = ma1_color, linewidth = 2, title='EMA 8', transp=0)
plot(display_ma ? ema(close,13) : na, color = ma2_color, linewidth = 2, title='EMA 13', transp=0)
plot(display_ma ? ema(close,21) : na, color = ma3_color, linewidth = 2, title='EMA 21', transp=0)
plot(display_ma ? ema(close,34) : na, color = ma4_color, linewidth = 2, title='EMA 34', transp=0)
plot(display_ma ? ema(close,55) : na, color = ma5_color, linewidth = 2, title='EMA 55', transp=0)
plot(display_ma ? ema(close,89) : na, color = ma6_color, linewidth = 2, title='EMA 89', transp=0)
plot(display_ma ? ema(close,144) : na, color = ma7_color, linewidth = 2, title='EMA 144', transp=0)
plot(display_ma ? ema(close,233) : na, color = ma8_color, linewidth = 2, title='EMA 233', transp=0)
plot(display_ma ? ema(close,377) : na, color = ma9_color, linewidth = 2, title='EMA 377', transp=0)
plot(display_ma ? ema(close,610) : na, color = ma10_color, linewidth = 2, title='EMA 610', transp=0)
plot(showvwap and isintraday ? vwap(close) : na, color=darkorange, linewidth=2, transp=0, title="VWAP")

//%BB Code starts
pbbmid = sma(close, bblen)
pdev = 2.0 * stdev(close, bblen)
pbbtop = pbbmid + pdev
pbblow = pbbmid - pdev
pbb = (close - pbblow) / (pbbtop - pbblow)
bbw = (pbbtop - pbblow)/pbbmid
xobot = crossunder(pbb, 0)
xotop = crossover(pbb, 1)
alertcondition(xotop, title="%BB > 1", message="%BB breached upwards")
alertcondition(xobot, title="%BB < 0", message="%BB breached downwards")
plotshape(showxobb ? pbb >= 1 : na, title="Dot mark Up", style=shape.triangleup, location=location.top, color=yellow, transp=0, size=size.tiny, editable=false)
plotshape(showxobb ? pbb <= 0 : na, title="Dot mark Down", style=shape.triangledown, location=location.bottom, color=red, transp=0, size=size.tiny, editable=false)
//%BB Code ends
